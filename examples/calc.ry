import("math.ry")

class Calculator {
    data input_str
    data pos

    func init(input) {
        this.input_str = input
        this.pos = 0
    }

    func peek() {
        if this.pos >= this.input_str.len { 
            return "EOF" 
        }
        return this.input_str[this.pos]
    }

    func to_number(s) {
        data res = 0.0
        data i = 0
        data divisor = 1.0
        data in_fraction = false

        while i < s.len {
            data c = s[i]
            if c == "." {
                in_fraction = true
            } else {
                data d = 0
                if c == "1" { d = 1 }
                else if c == "2" { d = 2 }
                else if c == "3" { d = 3 }
                else if c == "4" { d = 4 }
                else if c == "5" { d = 5 }
                else if c == "6" { d = 6 }
                else if c == "7" { d = 7 }
                else if c == "8" { d = 8 }
                else if c == "9" { d = 9 }

                if in_fraction {
                    divisor = divisor * 10.0
                    res = res + (d / divisor)
                } else {
                    res = res * 10.0 + d
                }
            }
            i = i + 1
        }
        return res
    }

    func parse_number() {
        data num_str = ""
        data c = this.peek()
        while (c >= "0" and c <= "9") or c == "." {
            num_str = num_str + c
            this.pos = this.pos + 1
            c = this.peek()
        }
        if num_str == "" {
            panic("Syntax Error: Expected a number or '(', but found '" + this.peek() + "'.")
        }
        return this.to_number(num_str)
    }

    func parse_identifier() {
        data id = ""
        data c = this.peek()
        while c >= "a" and c <= "z" {
            id = id + c
            this.pos = this.pos + 1
            c = this.peek()
        }
        return id
    }

    func factor() {
        data c = this.peek()
        if c == "(" {
            this.pos = this.pos + 1 # consume '('
            data result = this.expr()
            this.pos = this.pos + 1 # consume ')'
            return result
        }
        if (c >= "0" and c <= "9") or c == "." {
            return this.parse_number()
        }
        if c >= "a" and c <= "z" {
            data id = this.parse_identifier()
            if id == "pi" { return Math.pi }
            if id == "e" { return Math.e }
            
            if this.peek() == "(" {
                this.pos = this.pos + 1 # consume '('
                data val = this.expr()
                this.pos = this.pos + 1 # consume ')'
                
                if id == "sin" { return Math.sin(val) }
                if id == "cos" { return Math.cos(val) }
                if id == "tan" { return Math.tan(val) }
                if id == "sqrt" { return Math.sqrt(val) }
                if id == "log" { return Math.log(val) }
                panic("Unknown function: " + id)
            }
            panic("Unknown constant or identifier: " + id)
        }
        panic("Unexpected character: " + c)
    }

    func exponential() {
        data result = this.factor()
        while this.peek() == "^" {
            this.pos = this.pos + 1
            result = Math.pow(result, this.factor())
        }
        return result
    }

    func term() {
        data result = this.exponential()
        while this.peek() == "*" or this.peek() == "/" or this.peek() == "%" {
            data op = this.peek()
            this.pos = this.pos + 1
            if op == "*" { result = result * this.factor() }
            if op == "/" { result = result / this.factor() }
            if op == "%" { result = result % this.factor() }
        }
        return result
    }

    func expr() {
        data result = this.term()
        while this.peek() == "+" or this.peek() == "-" {
            data op = this.peek()
            this.pos = this.pos + 1
            if op == "+" { result = result + this.term() }
            if op == "-" { result = result - this.term() }
        }
        return result
    }

    func solve() {
        data result = this.expr()
        if this.peek() != "EOF" {
            panic("Syntax Error: Invalid character '" + this.peek() + "' at end of expression.")
        }
        return result
    }
}

# Main execution
data isRunning = true
data myCalc = null
data answer = null
data line = ""

out("Calculator - Ry version")
while isRunning {
    line = input(">> ")
    if line == "quit" { 
        isRunning = false 
    } else if line == "" {
        # Do nothing, just show a new prompt
    } else {
        attempt {
            myCalc = Calculator(line)
            answer = myCalc.solve()
            out(answer)
        } fail err {
            out(err)
        }
    }
}